
set data style lines
set key left

set term png small
set out 'mobius.png'

#######################################################
# This generates the figure for the mink-exact paper

set term postscript
set out 'mobius.ps'

set yrange[0:0.5]
set xrange[0:1]

# This fits A_3
b3(x) = (0<=x && x<1.0/5) ? (x/(1-3*x)) : \
        (1.0/5<=x && x<1.0/4) ? ((1-4*x)/(1-3*x)) : \
        (1.0/4<=x && x<2.0/7) ? ((4*x-1)/(x)) : \
        (2.0/7<=x && x<1.0/3) ? ((1-3*x)/(x)) : \
        (1.0/3<=x && x<3.0/8) ? ((3*x-1)/(1-2*x)) : \
        (3.0/8<=x && x<2.0/5) ? ((2-5*x)/(1-2*x)) : \
        (2.0/5<=x && x<3.0/7) ? ((2-5*x)/(1-3*x)) : \
        (3.0/7<=x && x<1.0/2) ? ((1-2*x)/(3*x-1)) : \
        0

A_3(x) = (0<=x && x<1.0/2) ? b3(x) : b3(1-x)


b2(x) = (0<=x && x<1.0/4) ? (x/(1-2*x)) : \
        (1.0/4<=x && x<1.0/3) ? ((1-3*x)/(1-2*x)) : \
        (1.0/3<=x && x<2.0/5) ? ((3*x-1)/(x)) : \
        (2.0/5<=x && x<1.0/2) ? ((1-2*x)/(x)) : \
        0

A_2(x) = (0<=x && x<1.0/2) ? b2(x) : b2(1-x)

b1(x) = (0<=x && x<1.0/3) ? (x/(1-x)) : \
        (1.0/3<=x && x<1.0/2) ? ((1-2*x)/(1-x)) : \
        0

A_1(x) = (0<=x && x<1.0/2) ? b1(x) : b1(1-x)

b0(x) = x

A_0(x) = (0<=x && x<1.0/2) ? b0(x) : b0(1-x)


set samples 2000

plot A_0(x), A_1(x), A_2(x), A_3(x)


